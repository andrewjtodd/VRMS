apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'gradle-one-jar'
apply plugin: 'application'

// Use Java 8 by default
sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0'

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar {
    manifest {
        attributes 'Main-Class': 'au.com.privitar.VRMSApp'
    }
}

// The main class of the application
mainClassName = 'au.com.privitar.VRMSApp'

// Add Gradle OneJarPlugin, see https:github.com/rholder/gradle-one-jar
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.3'
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '0.9.2'
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'io.dropwizard:dropwizard-migrations:' + dropwizardVersion,
            'io.dropwizard:dropwizard-auth:' + dropwizardVersion,
            'org.codehaus.groovy:groovy-all:2.3.11',
            'org.mongojack:mongojack:2.1.0',
            'org.mongodb:mongo-java-driver:2.12.3'
    )
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Configure the oneJar task
task oneJar(type: OneJar) {
    mainClass = mainClassName
    archiveName = 'vrms.jar'
    mergeManifestFromJar = true
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', 'config.yml'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

artifacts {
    oneJar
}